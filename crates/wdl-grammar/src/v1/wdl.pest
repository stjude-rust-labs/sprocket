// ========== //
// Whitespace //
// ========== //

// Pest provides relatively good support for whitespace out of the box. However,
// we decided that we are our parse tree to include details on the specific
// tokens that are used—to differentiate between spaces, tabs, newlines,
// carriage return-newlines, and then carriage returns. In this way, we can
// examine the parse tree when writing our linter.
SPACE  =  { " " }
TAB    =  { "\t" }
INDENT = _{ SPACE | TAB }

NEWLINE                 =  { "\n" }
CARRIAGE_RETURN_NEWLINE =  { "\r\n" }
CARRIAGE_RETURN         =  { "\r" }
LINE_ENDING             = _{ NEWLINE | CARRIAGE_RETURN_NEWLINE | CARRIAGE_RETURN }

WHITESPACE = ${ LINE_ENDING | INDENT }

// ======== //
// Comments //
// ======== //

COMMENT = { "#" ~ (!LINE_ENDING ~ ANY)* }

// ===== //
// Atoms //
// ===== //

OPTION      = { "?" }
ONE_OR_MORE = { "+" }
COMMA       = { "," }

// ======== //
// Literals //
// ======== //

// None.
none = ${ "None" ~ !ASCII_ALPHANUMERIC }

// Null.
null = ${ "null" ~ !ASCII_ALPHANUMERIC }

// Boolean.
boolean = ${ "true" ~ !ASCII_ALPHANUMERIC | "false" ~ !ASCII_ALPHANUMERIC }

// Integers.
integer_decimal = @{ "0" ~ !ASCII_ALPHANUMERIC | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
integer_hex     = @{ "0" ~ ("x" | "X") ~ ASCII_HEX_DIGIT+ }
integer_octal   = @{ "0" ~ ASCII_OCT_DIGIT+ }
integer         = ${ integer_decimal | integer_hex | integer_octal }

// Float.
float_e               = @{ ("e" | "E") ~ ("-" | "+")? ~ ASCII_DIGIT+ }
float_with_decimal    = @{ ASCII_DIGIT+? ~ "." ~ ASCII_DIGIT+ ~ float_e? }
float_without_decimal = @{ ASCII_DIGIT+ ~ "." ~ !ASCII_DIGIT ~ float_e? }
float_simple          = @{ ASCII_DIGIT+ ~ float_e }
float                 = ${ float_with_decimal | float_without_decimal | float_simple }

// Number.
// 
// NOTE: all rules included in `number` must be marked as atomic (`@`) or
// compound-atomic (`$`). This is not checked by the compiler, so you must
// ensure it remains true.
number = _{ float | integer }

// Character.
// 
// LENIENT: the `char_escaped_invalid` rule (and its inclusion in the
// `char_escaped` rule) are not to spec. However, we would like to support
// parsing of these invalid string characters so that we can return lint errors
// for them (rather than fail parsing, which returns a relatively inflexible
// error message). As such, if you write your own grammar and don't plan to
// handle this post parsing, you can remove the `char_escaped_invalid` rule from
// the `char_escaped` rule.
char_escaped_invalid = @{ "\\" ~ ANY }
char_escaped         = ${ "\\" ~ ("\\" | "\"" | "\'" | "n" | "r" | "b" | "t" | "f" | "a" | "v" | "?") }
char_octal           = @{ "\\" ~ ASCII_OCT_DIGIT{1, 3} ~ !ASCII_OCT_DIGIT }
char_hex             = @{ "\\x" ~ ASCII_HEX_DIGIT+ }
char_unicode_four    = @{ "\\" ~ ("u" | "U") ~ ASCII_HEX_DIGIT{4} ~ !ASCII_HEX_DIGIT }
char_unicode_eight   = @{ "\\" ~ ("u" | "U") ~ ASCII_HEX_DIGIT{8} ~ !ASCII_HEX_DIGIT }
char_unicode         = ${ char_unicode_four | char_unicode_eight }
char_special         = ${ char_escaped | char_hex | char_unicode | char_octal | char_escaped_invalid }
char_other           = @{ !("\\" | "\n") ~ ANY }

// String.
double_quote                        = { "\"" }
single_quote                        = { "\'" }
string_expression_placeholder_start = { "~{" | "${" }
string_expression_placeholder_end   = { "}" }

string_literal_contents = @{
    // NOTE: the `PEEK` here looks at what quoting is being used (double or
    // single) and denies any use of the character in literal contents. The
    // presence of `PEEK` implies that this rule must be embedded within a rule
    // that `PUSH`es these tokens on the stack.
    (!string_expression_placeholder_start ~ !PEEK ~ (char_special | char_other))+
}

string_expression_placeholder_expression = { string_placeholder | expression }

string_placeholder = ${
    string_expression_placeholder_start ~ placeholder_options* ~ string_expression_placeholder_expression ~ string_expression_placeholder_end
}

// NOTE: all rules included in `string` must be marked as atomic (`@`) or
// compound-atomic (`$`). This is because we don't want rules eating up
// whitespace within a string. This is not checked by the compiler, so you must
// ensure it remains true.
string_inner = ${ (string_placeholder | string_literal_contents)* }
string       = ${
    PUSH(double_quote | single_quote) ~ string_inner ~ POP
}

// Identifiers.
singular_identifier  = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
qualified_identifier = ${ singular_identifier ~ ("." ~ singular_identifier)+ }

// Literal.
literal = _{ boolean | number | string | none | singular_identifier }

// =========== //
// Expressions //
// =========== //

// Prefix.
or        = { "||" }
and       = { "&&" }
add       = { "+" }
sub       = { "-" }
mul       = { "*" }
div       = { "/" }
remainder = { "%" }
eq        = { "==" }
neq       = { "!=" }
lte       = { "<=" }
gte       = { ">=" }
lt        = { "<" }
gt        = { ">" }

infix = _{ or | and | add | sub | mul | div | remainder | eq | neq | lte | gte | lt | gt }

// Prefix.
negation              = { "!" }
unary_signed_positive = { "+" }
unary_signed_negative = { "-" }

prefix = _{ negation | unary_signed_positive | unary_signed_negative }

// Postfix.
member = ${ "." ~ singular_identifier }
index  = !{ "[" ~ expression ~ "]" }
call   = !{ "(" ~ (expression ~ (COMMA ~ expression)* ~ COMMA?)* ~ ")" }

postfix = _{ member | index | call }

// Core elements.
identifier_based_kv_key  = { singular_identifier }
expression_based_kv_key  = { expression }
kv_value                 = { expression }
identifier_based_kv_pair = { identifier_based_kv_key ~ ":" ~ kv_value }
expression_based_kv_pair = { expression_based_kv_key ~ ":" ~ kv_value }

group = !{ "(" ~ expression ~ ")" }
if    = ${
    "if" ~ (WHITESPACE | COMMENT)+ ~ expression ~ (WHITESPACE | COMMENT)+ ~ "then" ~ (WHITESPACE | COMMENT)+ ~ expression ~ (WHITESPACE | COMMENT)+ ~ "else" ~ (WHITESPACE | COMMENT)+ ~ expression
}

// DIVERGE: for the object literal, struct literal, map literal, and array
// literal rules below, a comma is required for any elements within those data
// structures that are not the last element.
// 
// Where commas are and are not required is a common source of confusion for
// users of WDL. Instead of enforcing that commas delimit these elements within
// the grammar (which returns a relatively inflexible error message), we have
// designated commas as optional here.
// 
// Validation rules exist to ensure that commas are present within these
// elements so that a nicer error message can be returned.
object_literal      = !{ "object" ~ "{" ~ (identifier_based_kv_pair ~ (COMMA? ~ identifier_based_kv_pair)* ~ COMMA?)* ~ "}" }
struct_literal_name =  { singular_identifier }
struct_literal      = !{ struct_literal_name ~ "{" ~ (identifier_based_kv_pair ~ (COMMA? ~ identifier_based_kv_pair)* ~ COMMA?)* ~ "}" }
map_literal         = !{ "{" ~ (expression_based_kv_pair ~ (COMMA? ~ expression_based_kv_pair)* ~ COMMA?)* ~ "}" }
array_literal       = !{ "[" ~ (expression ~ (COMMA? ~ expression)* ~ COMMA?)* ~ "]" }
pair_literal        = !{ "(" ~ expression ~ COMMA ~ expression ~ ")" }

atom = _{
    group
  | if
  | object_literal
  | struct_literal
  | map_literal
  | array_literal
  | pair_literal
  | literal
}

// Expression.
// 
// NOTE: this rule is defined as compound-atomic (`$`) for the following reason:
// 
// Whitespace is a problem when your rule ends with an optional element. Many of
// these expressions do, which means, when the element is not there, the
// expression consumes all of the whitespace that falls of the end of the
// statement. This is not a good practice, so we manually define where the
// whitespace can be in these cases.
// 
// As such, you will see that none of the permutations of the rule below end in
// an optional token. That is by design to avoid the problem above.
expression = ${
    prefix* ~ (WHITESPACE | COMMENT)* ~ atom ~ (WHITESPACE | COMMENT)* ~ postfix* ~ ((WHITESPACE | COMMENT)* ~ infix ~ (WHITESPACE | COMMENT)* ~ prefix* ~ (WHITESPACE | COMMENT)* ~ atom ~ (WHITESPACE | COMMENT)* ~ postfix+ | (WHITESPACE | COMMENT)* ~ infix ~ (WHITESPACE | COMMENT)* ~ prefix* ~ (WHITESPACE | COMMENT)* ~ atom)+
  | prefix* ~ (WHITESPACE | COMMENT)* ~ atom ~ (WHITESPACE | COMMENT)* ~ postfix+
  | prefix* ~ (WHITESPACE | COMMENT)* ~ atom
}

// Types.
// 
// NOTE: techically the spec calls the optional `+` the "non-empty" operator.
// Since we have already defined this as the "one or more" operator and they
// mean effectively the same thing, I've just kept this nomeclature.
array_type = ${
    "Array" ~ (WHITESPACE | COMMENT)* ~ (("[" ~ (WHITESPACE | COMMENT)* ~ wdl_type_inner ~ (WHITESPACE | COMMENT)* ~ "]" ~ ONE_OR_MORE) | ("[" ~ (WHITESPACE | COMMENT)* ~ wdl_type_inner ~ (WHITESPACE | COMMENT)* ~ "]"))
}
// NOTE: The `map_type` and `pair_type` rules **must** be marked as non-atomic, as the
// `unbound_declaration` and `bound_declaration` rules that use them are marked
// as compound-atomic.
map_type    = !{ "Map" ~ "[" ~ wdl_type_inner ~ COMMA ~ wdl_type_inner ~ "]" }
pair_type   = !{ "Pair" ~ "[" ~ wdl_type_inner ~ COMMA ~ wdl_type_inner ~ "]" }
string_type =  { "String" ~ !ASCII_ALPHANUMERIC }
file_type   =  { "File" ~ !ASCII_ALPHANUMERIC }
bool_type   =  { "Boolean" ~ !ASCII_ALPHANUMERIC }
int_type    =  { "Int" ~ !ASCII_ALPHANUMERIC }
float_type  =  { "Float" ~ !ASCII_ALPHANUMERIC }
object_type =  { "Object" ~ !ASCII_ALPHANUMERIC }

// NOTE: this rule was created separately from the below `wdl_type` rule to
// address the situation outlined in the comment there. In short, when a WDL
// type is embedded in things like a map or an array, you don't want to require
// that there is a space following the identifier.
wdl_type_inner = ${
    (map_type ~ OPTION)
  | (array_type ~ OPTION)
  | (pair_type ~ OPTION)
  | (string_type ~ OPTION)
  | (file_type ~ OPTION)
  | (bool_type ~ OPTION)
  | (int_type ~ OPTION)
  | (float_type ~ OPTION)
  | (object_type ~ OPTION)
  | (singular_identifier ~ OPTION)
  | map_type
  | array_type
  | pair_type
  | string_type
  | file_type
  | bool_type
  | int_type
  | float_type
  | object_type
  | singular_identifier
}

// NOTE: this rule requires a positive predicate whitespace because there
// **must** be a whitespace between the `wdl_type` and the `identifier` for
// `bound_declaration`s and `unbound_declaration`s. Else, you get weird things
// happening in these rules.
// 
// For example, when considering `IntermediateFiles`, `Int` matching the integer
// `wdl_type` and `ermediateFiles` matching the `identifier`.
struct_type =  { singular_identifier }
wdl_type    = ${
    (map_type ~ OPTION ~ &WHITESPACE)
  | (array_type ~ OPTION ~ &WHITESPACE)
  | (pair_type ~ OPTION ~ &WHITESPACE)
  | (string_type ~ OPTION ~ &WHITESPACE)
  | (file_type ~ OPTION ~ &WHITESPACE)
  | (bool_type ~ OPTION ~ &WHITESPACE)
  | (int_type ~ OPTION ~ &WHITESPACE)
  | (float_type ~ OPTION ~ &WHITESPACE)
  | (object_type ~ OPTION ~ &WHITESPACE)
  | (struct_type ~ OPTION ~ &WHITESPACE)
  | (map_type ~ &WHITESPACE)
  | (array_type ~ &WHITESPACE)
  | (pair_type ~ &WHITESPACE)
  | (string_type ~ &WHITESPACE)
  | (file_type ~ &WHITESPACE)
  | (bool_type ~ &WHITESPACE)
  | (int_type ~ &WHITESPACE)
  | (float_type ~ &WHITESPACE)
  | (object_type ~ &WHITESPACE)
  | (struct_type ~ &WHITESPACE)
}

unbound_declaration_name = { singular_identifier }
unbound_declaration      = { wdl_type ~ unbound_declaration_name }

bound_declaration_name = { singular_identifier }
bound_declaration      = { wdl_type ~ bound_declaration_name ~ "=" ~ expression }

declaration = _{
    bound_declaration
  | unbound_declaration
}

struct_name = { singular_identifier }
struct      = { "struct" ~ struct_name ~ "{" ~ (unbound_declaration)* ~ "}" }

// Imports.
import_uri        =  { string }
import_as         = ${ "as" ~ (WHITESPACE | COMMENT)+ ~ singular_identifier }
import_alias_from =  { singular_identifier }
import_alias_to   =  { singular_identifier }
import_alias      = ${
    "alias" ~ (WHITESPACE | COMMENT)+ ~ import_alias_from ~ (WHITESPACE | COMMENT)+ ~ "as" ~ (WHITESPACE | COMMENT)+ ~ import_alias_to
}
import            = ${
    "import" ~ (WHITESPACE | COMMENT)+ ~ import_uri ~ ((WHITESPACE | COMMENT)+ ~ import_as)? ~ ((WHITESPACE | COMMENT)+ ~ import_alias)*
}

// ============================== //
// Common Workflow/Tasks Elements //
// ============================== //

// NOTE: the specification states the following in the workflow section:
// 
// Tasks and workflows have several elements in common. These sections have
// nearly the same usage in workflows as they do in tasks, so we just link to
// their earlier descriptions.
// 
// - Inputs.
// - Private declarations.
// - Outputs.
// - Metadata.
// - Parameter metadata.
// 
// As such, we have used a common set of elements within the two (listed below).

// Common input declaration.
input = { "input" ~ "{" ~ (declaration)* ~ "}" }

// Common output declaration.
output = { "output" ~ "{" ~ (bound_declaration)* ~ "}" }

// Common private declarations.
private_declarations = { (bound_declaration)+ }

// Common metadata parameters.
metadata_key = { singular_identifier }
metadata_kv  = { metadata_key ~ ":" ~ metadata_value }

metadata_value = {
    string
  | number
  | boolean
  | null
  | metadata_object
  | metadata_array
}

metadata_object = { ("{" ~ "}") | "{" ~ metadata_kv ~ (COMMA ~ metadata_kv)* ~ COMMA? ~ "}" }
metadata_array  = { ("[" ~ "]") | "[" ~ metadata_value ~ (COMMA ~ metadata_value)* ~ COMMA? ~ "]" }

metadata           = { "meta" ~ "{" ~ (metadata_kv ~ !COMMA)* ~ "}" }
parameter_metadata = { "parameter_meta" ~ "{" ~ (metadata_kv ~ !COMMA)* ~ "}" }

// ===== //
// Tasks //
// ===== //

// Task runtimes.
task_runtime_mapping_value = { expression }
task_runtime_mapping_key   = { singular_identifier }
task_runtime_mapping       = { task_runtime_mapping_key ~ ":" ~ task_runtime_mapping_value }
task_runtime               = { "runtime" ~ "{" ~ (task_runtime_mapping)* ~ "}" }

// Expression placeholder options.
// 
// Expression placeholder options can be used anywhere where expression
// placeholders are evaluated, including (but not limited to):
// 
// * Strings
// 
// DIVERGE: the specification states that any expression placeholder conforms to
// the pattern `option="value"`. However, it is clear from the examples in the
// spec that single quoted strings are also allowed. Thus, we allow for either
// single or double quoted strings here—we will leave the selection of which to
// use up to a linting question.
// 
// LENIENT: the specification is pretty clear that no spaces are allowed between
// the option and the equals sign or the equals sign and the value. However,
// many tools choose to allow spaces here. As such, we will allow spaces in
// between these elements, but we will throw a lint warning for these cases.
placeholder_option_sep     = { "sep" ~ "=" ~ string }
placeholder_option_boolean = { boolean ~ "=" ~ string }
placeholder_option_default = { "default" ~ "=" ~ string }

placeholder_option  = {
    placeholder_option_sep
  | placeholder_option_boolean
  | placeholder_option_default
}
placeholder_options = { placeholder_option+ }

// Task commands, curly.
command_curly_begin = !{ "command" ~ "{" }
command_curly_end   =  { "}" }

command_curly_expression_placeholder_start = _{ "~{" | "${" }
command_curly_expression_placeholder_end   = _{ "}" }

command_curly_literal_contents = _{
    (!command_curly_begin ~ !command_curly_end ~ !command_curly_expression_placeholder_start ~ ANY)+
}

command_curly_expression_placeholder_expression = { command_curly_placeholder | expression }

command_curly_placeholder = !{
    command_curly_expression_placeholder_start ~ placeholder_options* ~ command_curly_expression_placeholder_expression ~ command_curly_expression_placeholder_end
}

command_curly_contents = {
    (command_curly_placeholder | command_curly_literal_contents)*
}

command_curly = ${
    command_curly_begin ~ command_curly_contents ~ command_curly_end
}

// Task commands, heredoc.
command_heredoc_begin                        = !{ "command" ~ "<<<" }
command_heredoc_end                          =  { ">>>" }
command_heredoc_expression_placeholder_start = _{ "~{" }
command_heredoc_expression_placeholder_end   = _{ "}" }

command_heredoc_literal_contents = _{
    (!command_heredoc_begin ~ !command_heredoc_end ~ !command_heredoc_expression_placeholder_start ~ ANY)+
}

command_heredoc_expression_placeholder_expression = { command_heredoc_placeholder | expression }

command_heredoc_placeholder = !{
    command_heredoc_expression_placeholder_start ~ placeholder_options* ~ command_heredoc_expression_placeholder_expression ~ command_heredoc_expression_placeholder_end
}

command_heredoc_contents = {
    (command_heredoc_placeholder | command_heredoc_literal_contents)*
}

command_heredoc = ${
    command_heredoc_begin ~ command_heredoc_contents ~ command_heredoc_end
}

task_command = { (command_heredoc | command_curly) }

// Task elements.
// 
// NOTE: all task elements except for `private_declarations` can be declared
// only once. This is not trivial to express as part of the grammar—especially
// since these elements can come in any order. As such, ensuring this remains
// true is left to postprocessing.
task_element = {
    input
  | output
  | task_command
  | task_runtime
  | private_declarations
  | parameter_metadata
  | metadata
}

task = { "task" ~ singular_identifier ~ "{" ~ task_element+ ~ "}" }

// ========= //
// Workflows //
// ========= //

// Workflow call
workflow_call_input = { (singular_identifier ~ "=" ~ expression) | singular_identifier }
// DIVERGE: the spec is ambiguous about whether whitespace is
// allowed between the opening and closing brackets (`{}`)
// when the call body is empty. We have opted to allow whitespace.
workflow_call_body  = !{
    "{" ~ ("input:" ~ workflow_call_input ~ (COMMA ~ workflow_call_input)*)? ~ COMMA? ~ "}"
}
workflow_call_as    = ${ "as" ~ (WHITESPACE | COMMENT)+ ~ singular_identifier }
workflow_call_after = ${ "after" ~ (WHITESPACE | COMMENT)+ ~ singular_identifier }
workflow_call_name  =  { qualified_identifier | singular_identifier }
workflow_call       = ${
    ("call" ~ (WHITESPACE | COMMENT)+ ~ workflow_call_name ~ ((WHITESPACE | COMMENT)+ ~ workflow_call_as)? ~ ((WHITESPACE | COMMENT)+ ~ workflow_call_after)* ~ ((WHITESPACE | COMMENT)* ~ workflow_call_body))
  | ("call" ~ (WHITESPACE | COMMENT)+ ~ workflow_call_name ~ ((WHITESPACE | COMMENT)+ ~ workflow_call_as)? ~ ((WHITESPACE | COMMENT)+ ~ workflow_call_after)+)
  | ("call" ~ (WHITESPACE | COMMENT)+ ~ workflow_call_name ~ (WHITESPACE | COMMENT)+ ~ workflow_call_as)
  | ("call" ~ (WHITESPACE | COMMENT)+ ~ workflow_call_name)
}

// Workflow scatter
workflow_scatter_iteration_statement_variable = { singular_identifier }
workflow_scatter_iteration_statement_iterable = { expression }
workflow_scatter_iteration_statement          = { "(" ~ workflow_scatter_iteration_statement_variable ~ "in" ~ workflow_scatter_iteration_statement_iterable ~ ")" }
workflow_scatter                              = {
    "scatter" ~ workflow_scatter_iteration_statement ~ "{" ~ workflow_execution_statement* ~ "}"
}

// Workflow conditional
workflow_conditional_condition = { expression }
workflow_conditional           = { "if" ~ "(" ~ workflow_conditional_condition ~ ")" ~ "{" ~ workflow_execution_statement* ~ "}" }

// Workflow execution statements
workflow_execution_statement = {
    (workflow_conditional | workflow_scatter | workflow_call | private_declarations)
}

// NOTE: all workflow elements except for `workflow_execution_statement` can be
// declared only once. This is not trivial to express as part of the
// grammar—especially since these elements can come in any order. As such,
// ensuring this remains true is left to postprocessing.
workflow_element = _{
    input
  | output
  | workflow_execution_statement
  | parameter_metadata
  | metadata
}

workflow_name = { singular_identifier }
workflow      = { "workflow" ~ workflow_name ~ "{" ~ workflow_element* ~ "}" }

// Document elements.
// 
// The element rule is marked as silent, as one typically only cares about the
// underlying statement (and not the fact that this was wrapped in a document
// element).
document_element = _{
    import
  | struct
  | task
}

// Document version.
version_release = @{ (ASCII_ALPHANUMERIC | "." | "-")+ }
version         =  { "version" ~ version_release }

// Document.
document = {
    SOI ~ (version) ~ (document_element)* ~ (workflow ~ document_element*)? ~ EOI
}
